evaluationDependsOn(':cmplibrary')
apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.sourcepointmeta.metaapp"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 2 
        versionName project(':cmplibrary').version
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = APP_NAME + ".apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testCoverageEnabled = false
        }
        debug{
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    def androidExclusion = [
            '**/databinding/**/*.*',
            '**/android/databinding/*Binding.*',
            '**/BR.*',
            '**/R.*',
            '**/R$*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*_MembersInjector.*',
            '**/Dagger*Component.*',
            '**/Dagger*Component$Builder.*',
            '**/*Module_*Factory.*',
            '**/*Fragment*.*',
            '**/*Activity*.*',
            '**/*Adapter*.*',
            '**/*ViewPager*.*',
            '**/*ViewHolder*.*',
            '**/*Module*.*'
    ]


    //task to create codeCoverage through jacoco
    task codeCoverageReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
        group = "Reporting"
        description = "Generate Jacoco coverage reports after running tests."
        reports {
            xml.enabled = true
            html.enabled = true
            csv.enabled false
        }
        classDirectories.setFrom( fileTree(
                dir: 'build/intermediates/classes/debug/com/com.sourcepoint.app',
                excludes: androidExclusion
        ))
        sourceDirectories.setFrom( files('src/main/java/com.sourcepoint.app'))
        executionData.setFrom( files('build/jacoco/testDebugUnitTest.exec'))
    }

    task getCoverage(type: Exec, dependsOn: 'codeCoverageReport') {
        group = "Reporting"
        commandLine "open", "${buildDir}/reports/jacoco/codeCoverageReport/html/index.html"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'

    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    // Set this dependency to build and run Espresso tests
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2') {
        exclude module: 'support-annotations'
    }
    // recycler view test
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2') {
        exclude module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    //activity rules for instrumentation test
    androidTestImplementation('com.android.support.test:rules:1.0.2') {
        exclude module: 'support-annotations'
    }
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.2.0'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:2.19.0'
    // Room (use 1.1.0-beta1 for latest beta)
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.1.1"
    // recycler view
    implementation "com.android.support:recyclerview-v7:28.0.0"
    //card view
    implementation "com.android.support:cardview-v7:28.0.0"
    //source point cmp library
    implementation project(':cmplibrary')
    // swipe reveal layout
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'

    //gson
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation "android.arch.core:core-testing:1.1.1"
}
