def VERSION_NAME = "5.3.11"

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply plugin: 'io.github.dryrum.update-changelog'
apply plugin: "io.github.dryrum.replace-in-file"
apply plugin: "io.github.dryrum.git-utils"

group = "com.sourcepoint.cmplibrary"
version = VERSION_NAME

android {
    compileSdkVersion 28
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 300
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug{
            buildConfigField "String", "LOGGER_URL", "\"https://wrapper-api.sp-prod.net/metrics/v1/custom-metrics\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "LOGGER_URL", "\"https://wrapper-api.sp-prod.net/metrics/v1/custom-metrics\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Unfortunately we depend on a outdated version of okhttp due to its support to older versions of Android
    //noinspection GradleDependency
    api 'com.squareup.okhttp3:okhttp:3.12.10'

    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.4.0'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.json:json:20180813'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    androidTestImplementation 'junit:junit:4.13'
}

task versionTxt{
    group = "release-utility"
    doLast{
        new File(projectDir, "version.txt").text = "$VERSION_NAME"
    }
}

apply from: '../gradleutils/publish-android.gradle'

changeLogConfig {
    changeLogPath = rootDir.path + "/CHANGELOG.md"
    content = file(rootDir.path + "/cmplibrary/release_note.txt").text
    version = "$VERSION_NAME"
}

replaceInFile {
    docs {
        println " ============================= " + rootDir.path + "/README.md"
        doc {
            path = rootDir.path + "/README.md"
            find = "com.sourcepoint.cmplibrary:cmplibrary:(\\d)+\\.(\\d)+\\.(\\d)+"
            replaceWith = "com.sourcepoint.cmplibrary:cmplibrary:$VERSION_NAME"
        }
    }
}

addCommitPushConfig {
    fileList = [
            rootDir.path + '/CHANGELOG.md',
            rootDir.path + '/README.md'
    ]
}